e MIT license.
 * For full license text, see LICENSE.md file in the repo root or
 * https://opensource.org/licenses/MIT
 */

var bananas : 0..100 = 2;
var notePresent: Boolean;
var noteCounter: 0..4 = 4;
type Person : either {
  Happy,
  Hungry,
  GoingToStore,
  ReturningFromStore {
    carrying: 1..8
  },
  Failed,
  Waiting
};
var roommates: Array<Person>[1..5];


rule step for state in roommates {
  match state {
    Happy {
      state = Hungry;
    }

    Hungry {
      if bananas == 0 {
        if (notePresent && noteCounter > 1) {
          noteCounter -= 1;
          state = Waiting;
        } else {
          notePresent = True;
          noteCounter = 4;
          state = GoingToStore;
        }
      } else {
        bananas -= 1;
        state = Happy;
      }
    }

    GoingToStore {
      state = ReturningFromStore {
        carrying: urandomRange(1, 8)
      };
    }

    ReturningFromStore(bag) {
      if (urandomRange(0, 2) == 0) {
        state = Failed;
      } else {
        bananas += bag.carrying;
        notePresent = False;
        state = Hungry;
      }
    }
    
    Failed {
      state = Hungry;
    }
    
    Waiting {
      if (bananas > 0 || !notePresent) {
        state = Hungry;
      }
    }
  }
}

invariant BananaLimit {
  assert bananas <= 8;
}
